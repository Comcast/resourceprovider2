buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    jcenter()
    google()
}

dependencies {
    implementation gradleApi()
    implementation 'com.squareup:javapoet:1.10.0'
    implementation 'com.squareup:kotlinpoet:1.0.0-RC1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compileOnly("com.android.tools.build:gradle:3.6.3")
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

group 'com.xfinity'
version '1.3.0'

//Local publishing for development purposes
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from    components.java
            groupId    project.group
            artifactId    "resourceprovider"
            version    project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/Comcast/resourceprovider2'
    vcsUrl = 'https://github.com/Comcast/resourceprovider2'
    tags = ['android', 'gradle', 'resources', 'kotlin', 'mvp']
}

gradlePlugin {
    plugins {
        resourceProviderPlugin {
            id = 'com.xfinity.resourceprovider'
            displayName = 'Resource Provider'
            description = 'Resource Management API Generator for Android'
            implementationClass = 'com.xfinity.resourceprovider.ResourceProviderPlugin'
        }
    }
}